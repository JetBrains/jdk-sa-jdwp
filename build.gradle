apply plugin: 'maven-publish'

allprojects {
    apply plugin: 'java'
}

task mainJar(type: Jar) {
    manifest {
        attributes("Main-Class": "com.jetbrains.sa.SaJdwp")
    }
    from project(':core').sourceSets.main.output
    from project(':compatibility-8').sourceSets.main.output
    from project(':compatibility-10').sourceSets.main.output
    from project(':compatibility-13').sourceSets.main.output
    baseName 'sa-jdwp'
}

task sourceJar(type: Jar) {
    from project(':core').sourceSets.main.java
    from project(':compatibility-8').sourceSets.main.java
    from project(':compatibility-10').sourceSets.main.java
    from project(':compatibility-13').sourceSets.main.java
    baseName 'sa-jdwp-sources'
}

artifacts {
    archives mainJar, sourceJar
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            artifactId 'sa-jdwp'
            groupId 'org.jetbrains.intellij.deps'
            version '1.16'
            artifact mainJar
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
    repositories {
        maven {
            url "https://packages.jetbrains.team/maven/p/ij/intellij-dependencies"
            credentials {
                username = System.getenv('INTELLIJ_DEPENDENCIES_BOT')
                password = System.getenv('INTELLIJ_DEPENDENCIES_TOKEN')
            }
        }
    }
}
